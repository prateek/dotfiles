function setup_for_ralph() {
  emulate -L zsh

  local cwd
  cwd="$(pwd)"

  local porcelain

  if ! git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
    print -u2 -- "setup_for_ralph: error: not a git repository: ${cwd}"
    return 1
  fi

  porcelain="$(git status --porcelain --untracked-files=all)"
  if [[ -n "${porcelain}" ]]; then
    print -u2 -- "setup_for_ralph: error: git repo must be clean (no staged/unstaged/untracked files)"
    print -u2 -- "setup_for_ralph: error: run 'git status' to inspect"
    return 1
  fi

  if [[ ! -f "PRD.md" ]]; then
    print -u2 -- "setup_for_ralph: error: PRD.md not found in ${cwd}"
    return 1
  fi

  if [[ ! -f ".gitignore" ]]; then
    print -r -- ".logs/" > .gitignore
  elif ! grep -qF ".logs/" .gitignore; then
    print -r -- "" >> .gitignore
    print -r -- ".logs/" >> .gitignore
  fi

  cat > ralph-loop.sh <<'RALPH_LOOP_EOF'
#!/bin/bash
set -e

# adapted from: https://gist.github.com/prateek/14fae59c71921710a3e055d74f30c8af
if [[ $# -lt 1 ]]; then
  echo "Usage: $0 <iterations>"
  exit 1
fi

if ! [[ "$1" =~ ^[0-9]+$ ]] || [[ "$1" -lt 1 ]]; then
  echo "Iterations must be a positive integer"
  exit 1
fi

PROMISE_FILE="I_PROMISE_ALL_TASKS_IN_THE_PRD_ARE_DONE_I_AM_NOT_LYING_I_SWEAR"

mkdir -p .logs
rm -f "$PROMISE_FILE"

for ((i = 1; i <= $1; i++)); do
  codex --dangerously-bypass-approvals-and-sandbox exec <<'EOF' 2>&1 | tee -a ".logs/iterations.log"
1. Find the highest-priority task based on PRD.md and progress.txt, and implement it.
2. Run your tests and type checks.
3. Update the PRD with what was done.
4. Append your progress to progress.txt.
5. Commit your changes.
ONLY WORK ON A SINGLE TASK.

If the PRD is complete, and there are NO tasks left, then and only then touch a file named I_PROMISE_ALL_TASKS_IN_THE_PRD_ARE_DONE_I_AM_NOT_LYING_I_SWEAR. Otherwise respond with a brief summary of changes/progress.
EOF

  if [[ -f "$PROMISE_FILE" ]]; then
    echo "PRD complete after $i iterations."
    exit 0
  fi
done

echo "PRD not complete after $1 iterations."
exit 1
RALPH_LOOP_EOF

  chmod +x ralph-loop.sh

  if [[ ! -f "progress.md" ]]; then
    print -r -- "$(date '+%Y-%m-%d %H:%M'): initial setup" > progress.md
  fi

  porcelain="$(git status --porcelain --untracked-files=all)"
  if [[ -n "${porcelain}" ]]; then
    git add -A
    git commit -m "chore: ralph setup"
  fi

  print -- "setup_for_ralph: success"
}

setup_for_ralph "$@"
